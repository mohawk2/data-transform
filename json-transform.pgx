# A simple grammar for JSON::Transform
%grammar json-transform
%version 0.01
%include pegex-atoms

# string and number from https://github.com/ingydotnet/json-pgx/blob/master/json.pgx

transforms: -transformation+

transformation: .(/-/) (
  transformImpliedDest
|
  transformCopy
|
  transformMove
|
  .ws2
) .(/-/)

transformImpliedDest: jsonPointer exprMapping

transformCopy: (jsonPointer | variableUser) .opCopyFrom -exprSingleValue exprMapping?

transformMove: jsonPointer .opMoveFrom jsonPointer

opCopyFrom: /- ( LANGLE DASH ) -/

opMoveFrom: /- ( LANGLE LANGLE ) -/

opFrom: opArrayFrom | opObjectFrom

opArrayFrom: /- ( LANGLE AT ) -/

opObjectFrom: /- ( LANGLE PERCENT ) -/

exprMapping: +opFrom (exprArrayMapping | exprObjectMapping)

exprArrayMapping: .(/- LSQUARE -/) -exprSingleValue .(/- RSQUARE -/)

exprObjectMapping: .(/- LCURLY -/) -colonPair .(/- RCURLY -/)

colonPair: exprStringValue .(/- COLON -/) exprSingleValue

exprSingleValue: (jsonPointer | variableUser | variableSystem) -singleValueMod?

singleValueMod: (exprKeyAdd | exprKeyRemove)

exprKeyAdd: .(/- PLUS -/) -colonPair

exprKeyRemove: .(/- DASH -/) -exprStringValue

exprStringValue: (jsonPointer | variableUser | variableSystem | exprStringQuoted)

exprStringQuoted: /
  GRAVE
    (
      (:
        jsonUnicode
          |
        jsonBackslashQuote
          |
        jsonBackslashDollar
          |
        BACK (:     # Backslash escapes
          [
            GRAVE  # Double Quote
          ]
        )
      |
        [^ GRAVE CONTROLS BACK ]  # Anything else
      )*
    )
  GRAVE
/

jsonPointer: /
  DOUBLE
    (
      (:
        jsonUnicode
          |
        jsonBackslashQuote
          |
        jsonBackslashDollar
          |
        BACK (:     # Backslash escapes
          [
            DOUBLE  # Double Quote
          ]
        )
      |
        [^ DOUBLE CONTROLS BACK ]  # Anything else
      )*
    )
  DOUBLE
/

jsonBackslashDollar: / BACK DOLLAR /

jsonBackslashQuote: /
  BACK [
    BACK    # Back Slash
    SLASH   # Forward Slash
    'b'     # Back Space
    'f'     # Form Feed
    'n'     # New Line
    'r'     # Carriage Return
    't'     # Horizontal Tab
  ]
/

jsonUnicode: / BACK 'u' HEX{4} / # Unicode octet pair

variableUser: .(/- DOLLAR/) /([ LOWERS ] [ ALPHAS ]*)/

variableSystem: .(/- DOLLAR/) /([ UPPERS ]*)/

ws: / (: WS | \x{FEFF} | comment ) /

comment: / BLANK* HASH BLANK* [^\r\n]* (: EOL | CR !NL | EOS ) / # CR is because MacOS 9
